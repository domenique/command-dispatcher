/*
 * Copyright 2015 TripleD framework.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  ext {
    springBootVersion = '2.1.7.RELEASE'
    springBomVersion = '2.0.8.RELEASE'
    springDepMgmtVersion = '1.0.8.RELEASE'
  }
  repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/plugins-release' }
    jcenter()
  }
  dependencies {
    classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1")
    classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
  }
}

// some global vars to determine the type of build.
ext {
  isDevBuild = false
  isCiBuild = false
  isReleaseBuild = false
  sonatypeRepositoryUrl = null
  sonatypeUsername = null
  sonatypePassword = null
}

apply plugin: 'com.github.kt3k.coveralls'

allprojects {
  apply plugin: 'jacoco'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'signing'
  apply plugin: 'maven'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  group = 'eu.tripled-framework'
  version = '0.0.2'

  //set build variables based on build type (release, continuous integration, development)
  sonatypeUsername = System.getenv('OSSRH_USERNAME')
  sonatypePassword = System.getenv('OSSRH_PASSWD')
  if (project.hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
  } else if (project.hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
  } else {
    version += "-DEV"
    isDevBuild = true
  }

  task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives javadocJar, sourcesJar
  }

  if (isReleaseBuild) {
    signing {
      sign configurations.archives
    }
  } else {
    task signArchives {
      // do nothing
    }
  }

  jacocoTestReport {
    getAdditionalSourceDirs().from(sourceSets.main.allSource.srcDirs)
    getSourceDirectories().from(sourceSets.main.allSource.srcDirs)
    getClassDirectories().from(sourceSets.main.output)
    reports {
      html.enabled = true
      xml.enabled = true
      csv.enabled = false
    }
  }
}

task jacocoRootReport(type: JacocoReport) {
  dependsOn = subprojects.test
  getAdditionalSourceDirs().from(subprojects.sourceSets.main.allSource.srcDirs)
  getSourceDirectories().from(subprojects.sourceSets.main.allSource.srcDirs)
  getClassDirectories().from(subprojects.sourceSets.main.output)
  getExecutionData().from(subprojects.jacocoTestReport.executionData)
  reports {
    html.enabled = true
    xml.enabled = true
    csv.enabled = false
  }
  onlyIf = {
    true
  }
  doFirst {
    getExecutionData().from(executionData.findAll {
      it.exists()
    })
  }
}


coveralls {
  sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
  jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
  group = 'Coverage reports'
  description = 'Uploads the aggregated coverage report to Coveralls'

  dependsOn jacocoRootReport
}
