/*
 * Copyright 2015 TripleD framework.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id "cn.bestwu.propdeps" version "0.0.10"
    id "cn.bestwu.propdeps-idea" version "0.0.10"
    id 'io.spring.dependency-management' version "${springDepMgmtVersion}"
}

compileJava.dependsOn(processResources)

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    compile project(":eventbus-core")
    compile("org.springframework.boot:spring-boot")
    compile("org.springframework.boot:spring-boot-autoconfigure")
    optional("org.springframework.boot:spring-boot-configuration-processor")

    testCompile("org.junit.jupiter:junit-jupiter:5.5.1")
    testCompile("org.hamcrest:hamcrest:2.1")
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else if (isReleaseBuild || isCiBuild) {
            mavenDeployer {
                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'TripleD framework'
                    packaging 'jar'
                    description 'TripleD is a framework to create CQRS based application using Java.'
                    url 'http://www.tripled-framework.eu/'

                    scm {
                        url 'scm:git@github.com:domenique/tripled-framework.git'
                        connection 'scm:git@github.com:domenique/tripled-framework.git'
                        developerConnection 'scm:git@github.com:domenique/tripled-framework.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'domenique'
                            name 'Domenique Tilleuil'
                        }
                    }
                }
            }
        }
    }
}
